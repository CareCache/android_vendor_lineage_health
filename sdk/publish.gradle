/*
 * Copyright (C) 2020 The LineageOS Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: "maven-publish"
apply plugin: "signing"

import org.lineageos.build.Config

def LIB_GROUP_ID = "org.lineageos"
def LIB_ARTIFACT_ID = "mod.health-sdk"
def LIB_VERSION = Config.versionName
def POM_LICENSE_NAME = "The Apache Software License, Version 2.0"
def POM_LICENSE_URL = "http://www.apache.org/licenses/LICENSE-2.0.txt"
def POM_DEV_ID = "lineageos"
def POM_DEV_NAME = "The LineageOS Project"


// Publish

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

artifacts {
    archives sourceJar
}

publishing {
    repositories {
        def localProps = new Properties()
        localProps.load(project.rootProject.file("local.properties").newDataInputStream())

        maven {
            name = "Sonatype"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = localProps.getProperty("maven_central.username", "")
                password = localProps.getProperty("maven_central.password", "")
            }
        }
        maven {
            name = "GithubPackages"
            url = localProps.getProperty("github.url", "")
            credentials {
                username = localProps.getProperty("github.username", "")
                password = localProps.getProperty("github.token", "")
            }
        }
        maven {
            name = "LocalMavenRepo"
            url = "file://${buildDir}/repo"
        }
    }
    publications {
        droidlib(MavenPublication) {
            groupId LIB_GROUP_ID
            artifactId LIB_ARTIFACT_ID
            version LIB_VERSION
            artifact("${buildDir}/outputs/aar/sdk-release.aar")
            artifact(sourceJar)

            pom.name = LIB_ARTIFACT_ID

            pom.licenses {
                license {
                    name = POM_LICENSE_NAME
                    url = POM_LICENSE_URL
                }
            }

            pom.developers {
                developer {
                    id = POM_DEV_ID
                    name = POM_DEV_NAME
                }
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode("dependencies")
                configurations.api.allDependencies.each {
                    def node = dependenciesNode.appendNode("dependency")
                    node.appendNode("groupId", it.group)
                    node.appendNode("artifactId", it.name)
                    node.appendNode("version", it.version)
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}
